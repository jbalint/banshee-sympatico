
prefix ledger: <https://github.com/jbalint/ledger-ontology#>

select ?category ?year
(sum(?amt_01) as ?amt_Jan)
(sum(?amt_02) as ?amt_Feb)
(sum(?amt_03) as ?amt_Mar)
(sum(?amt_04) as ?amt_Apr)
(sum(?amt_05) as ?amt_May)
(sum(?amt_06) as ?amt_Jun)
(sum(?amt_07) as ?amt_Jul)
(sum(?amt_08) as ?amt_Aug)
(sum(?amt_09) as ?amt_Sep)
(sum(?amt_10) as ?amt_Oct)
(sum(?amt_11) as ?amt_Nov)
(sum(?amt_12) as ?amt_Dec)
from <http://banshee-sympatico/ledger>
{
  #bind(2 as ?input_month)
  #filter(?month = ?input_month)
  bind(2020 as ?input_year)
  filter(?year = ?input_year)
  {
    ?tx a ledger:Payment ; ledger:payee ?payee .
    ?tx ledger:time ?time ; ledger:post ?p.
    ?p ledger:amount ?pay_amt ; ledger:category ?category
    bind(year(?time) as ?year)
    bind(month(?time) as ?month)
    bind(?pay_amt * -1 as ?amt)
  }
  UNION
  {
    ?tx a ledger:Income .
    ?tx ledger:time ?time ; ledger:post ?p.
    ?p ledger:amount ?amt ; ledger:category ?category
    bind(year(?time) as ?year)
    bind(month(?time) as ?month)
  }
  FILTER NOT EXISTS {?tx ledger:note ?note FILTER(contains(?note, "[XJ]"))}
  bind(if(?month = 1, ?amt, 0) as ?amt_01)
  bind(if(?month = 2, ?amt, 0) as ?amt_02)
  bind(if(?month = 3, ?amt, 0) as ?amt_03)
  bind(if(?month = 4, ?amt, 0) as ?amt_04)
  bind(if(?month = 5, ?amt, 0) as ?amt_05)
  bind(if(?month = 6, ?amt, 0) as ?amt_06)
  bind(if(?month = 7, ?amt, 0) as ?amt_07)
  bind(if(?month = 8, ?amt, 0) as ?amt_08)
  bind(if(?month = 9, ?amt, 0) as ?amt_09)
  bind(if(?month = 10, ?amt, 0) as ?amt_10)
  bind(if(?month = 11, ?amt, 0) as ?amt_11)
  bind(if(?month = 12, ?amt, 0) as ?amt_12)
}
group by ?category ?year
order by ?category

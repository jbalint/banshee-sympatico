plugins {
    id "com.github.johnrengelman.shadow" version "1.2.4"
}

version = '0.1'

apply plugin: "java"
apply plugin: "idea"

repositories {
    mavenCentral()
    mavenLocal()
    maven {
        url "http://maven.stardog.com"
    }
}

dependencies {
    compile "com.google.guava:guava:18.0"
  compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.8'
  compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.8'
    compile group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.8'
    //compile "xml-apis:xml-apis:1.4.01"
    // TODO : don't need the entire server here, just client and some RDF utils
    compile "com.complexible.stardog:client-http:6.0.1"
    testCompile "junit:junit:4.12"
    // both published locally
    compile ("org.grobid:grobid-core:0.4.2-SNAPSHOT") {
        // pulling in v1.2
        exclude group: "log4j", module: "log4j"
        exclude group: "org.slf4j", module: "slf4j-log4j12"
    }
    compile "be.ugent.mmlab.rml:RML-Mapper:1.1"
    compile ("be.ugent.mmlab.rml:RML-MapDocHandler:1.1-SNAPSHOT") {
        exclude group: "org.openrdf.sesame"
        exclude group: "log4j", module: "log4j"
        exclude group: "org.slf4j", module: "slf4j-log4j12"
    }

    compile ("be.ugent.mmlab.rml:RML-Processor:0.3") {
        exclude group: "org.openrdf.sesame"
        exclude group: "log4j", module: "log4j"
        exclude group: "org.slf4j", module: "slf4j-log4j12"
    }
    // To replace Sesame in RML-Mapper AND be compatible with Stardog on Sesame 4.0
    compile "org.openrdf.sesame:sesame-runtime:4.0.0"
}
configurations.all {
 resolutionStrategy {
  force "xml-apis:xml-apis:1.4.01"
  // force these so all Sesame and crap incl Stardog lines up the same version
  force "org.apache.httpcomponents:httpclient:4.5.2"
  force "org.apache.httpcomponents:httpmime:4.5.2"
  force "org.apache.httpcomponents:httpclient-osgi:4.5.2"
  force "org.apache.httpcomponents:httpcore:4.4.4"
  force "org.apache.httpcomponents:httpcore-nio:4.4.4"
  force "org.apache.httpcomponents:httpcore-osgi:4.4.4"
 }
}
apply plugin: "application"

mainClassName = "com.jbalint.bs.bookstore.FileMetadataGenerator"

run {
    args "$db", "$root", "$graph"
}
